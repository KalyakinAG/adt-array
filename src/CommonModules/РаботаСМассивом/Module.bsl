Функция Массив(НовыеЭлементы) Экспорт
	Элементы = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(НовыеЭлементы);
	Возврат Элементы;
КонецФункции

//  find
Функция НайтиЭлемент(Элементы, Предикат, Контекст = Неопределено) Экспорт
	Для Каждого Элемент Из Элементы Цикл
		Если Вычислить(Предикат) Тогда
			Возврат Элемент;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

//  filter
Функция Отобрать(Элементы, Предикат, Контекст = Неопределено) Экспорт
	НайденныеЭлементы = Новый Массив;
	Для Каждого Элемент Из Элементы Цикл
		Если Вычислить(Предикат) Тогда
			НайденныеЭлементы.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Возврат НайденныеЭлементы;
КонецФункции

//  map
Функция Отобразить(Элементы, Выражение, Контекст = Неопределено, Данные = Неопределено) Экспорт
	НовыеЭлементы = Новый Массив;
	Для Каждого Элемент Из Элементы Цикл
		НовыеЭлементы.Добавить(Вычислить(Выражение));
	КонецЦикла;
	Возврат НовыеЭлементы;
КонецФункции

//  reduce
Функция Преобразовать(Элементы, Выражение, НачальноеЗначение = Неопределено, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Накопитель = НачальноеЗначение;
	Для Каждого Элемент Из Элементы Цикл
		Накопитель = Вычислить(Выражение);
	КонецЦикла;
	Возврат Накопитель;
КонецФункции

//  pop
Функция Взять(Элементы) Экспорт
	ВГраница = Элементы.ВГраница();
	Если ВГраница = -1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Результат = Элементы[ВГраница];
	Элементы.Удалить(ВГраница);
	Возврат Результат;
КонецФункции

//  push
Функция Положить(Элементы, Элемент) Экспорт
	Элементы.Добавить(Элемент);
КонецФункции

//  forEach
Функция ДляКаждого(Элементы, Алгоритм, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Для Каждого Элемент Из Элементы Цикл
		Выполнить(Алгоритм);
	КонецЦикла;
КонецФункции

// Быстрая сортировка
// http://ru.wikibooks.org/wiki/%D0%9F%D1%80%D0%B8%D0%BC%D0%B5%D1%80%D1%8B_%D1%80%D0%B5%D0%B0%D0%BB%D0%B8%D0%B7%D0%B0%D1%86%D0%B8%D0%B8_%D0%B1%D1%8B%D1%81%D1%82%D1%80%D0%BE%D0%B9_%D1%81%D0%BE%D1%80%D1%82%D0%B8%D1%80%D0%BE%D0%B2%D0%BA%D0%B8
// Реализация взята из публикации https://infostart.ru/1c/articles/204320/ 
// 
// Параметры:
//  Элементы Элементы
//  Предикат - Строка, Неопределено - Предикат
//  Контекст - Неопределено - Контекст
//  НижнийПредел - Число - Нижний предел
//  ВерхнийПредел - Число - Верхний предел
Процедура БыстраяСортировка(Элементы, Предикат, Контекст, НижнийПредел, ВерхнийПредел)
    Начало = НижнийПредел;
    Конец = ВерхнийПредел;
    _2 = Элементы[Цел((Начало + Конец)/2)];
	Пока Истина Цикл
		_1 = Элементы[Начало];
        Пока Вычислить(Предикат) < 0 Цикл // Элементы[Начало] < _2
            Начало = Начало + 1;                   
			_1 = Элементы[Начало];
		КонецЦикла;
		_1 = Элементы[Конец];
        Пока Вычислить(Предикат) > 0 Цикл // Элементы[Конец] > _2
            Конец = Конец - 1;                   
			_1 = Элементы[Конец];
        КонецЦикла; 
        Если Начало <= Конец Тогда               
            Значение = Элементы[Начало];
            Элементы[Начало] = Элементы[Конец];
            Элементы[Конец] = Значение;
            Начало = Начало + 1;
            Конец = Конец - 1;            
        КонецЕсли;
        Если Начало > Конец Тогда                       
            Прервать;                        
        КонецЕсли;
	КонецЦикла;
    Если НижнийПредел < Конец Тогда         
        БыстраяСортировка(Элементы, Предикат, Контекст, НижнийПредел, Конец);        
	КонецЕсли; 
    Если Начало < ВерхнийПредел Тогда                      
        БыстраяСортировка(Элементы, Предикат, Контекст, Начало, ВерхнийПредел);        
    КонецЕсли;
КонецПроцедуры

Функция Сравнить(_1, _2) Экспорт
	Если _1 = _2 Тогда
		Возврат 0;
	КонецЕсли;
	Если _1 < _2 Тогда
		Возврат -1;
	КонецЕсли;
	Возврат 1;
КонецФункции

//  sort
Процедура Сортировать(Элементы, НовыйПредикат = Неопределено, Контекст = Неопределено) Экспорт
	Предикат = ?(ЗначениеЗаполнено(НовыйПредикат), НовыйПредикат, "Сравнить(_1, _2)");
	БыстраяСортировка(Элементы, Предикат, Контекст, 0, Элементы.ВГраница());
КонецПроцедуры
