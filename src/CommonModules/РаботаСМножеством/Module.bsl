//  Подсистема "Абстрактный тип данных массив"
//	Автор: Калякин Андрей Г.
//  https://github.com/KalyakinAG/adt-array
//  https://infostart.ru/1c/articles/1473034/

#Область ПрограммныйИнтерфейс

// Множество.
// 
// Параметры:
//  Ключ - Строка, Неопределено - список полей ключа через запятую. 
//  			Если не задан, то ключом будет сам элемент множества. Имя поля ключа должно
//  			удовлетворять требованиям имен переменных
// 
// Возвращаемое значение:
//  Структура - Множество:
// * Ключ - Строка, Неопределено - 
// * Словарь - Соответствие из Произвольный - рекурсивный словарь
// * СоставКлюча - Неопределено, Массив из Строка - состав полей ключа
Функция Множество(Ключ = Неопределено) Экспорт
	
	ОбъектМножество = Новый Структура;
	ОбъектМножество.Вставить("Ключ", Ключ);
	ОбъектМножество.Вставить("Словарь", Новый Соответствие);
	
	Если ТипЗнч(Ключ) = Тип("Строка") Тогда
		ОбъектМножество.Вставить("СоставКлюча", РаботаСМассивом.Массив(Ключ));
	Иначе
		ОбъектМножество.Вставить("СоставКлюча", Неопределено);
	КонецЕсли;
	
	Возврат ОбъектМножество;
	
КонецФункции

// Дополнить.
// 
// Параметры:
//  Множество - см. Множество
//  Элементы - Массив из Произвольный
// 
// Возвращаемое значение:
// - см. Множество
Функция Дополнить(Множество = Неопределено, Элементы) Экспорт
	
	ОбъектМножество = ?(Множество = Неопределено ИЛИ ТипЗнч(Множество) = Тип("Строка"), Множество(Множество), Множество);
	
	Для Каждого Элемент Из Элементы Цикл
		Добавить(ОбъектМножество, Элемент);
	КонецЦикла;
	
	Возврат ОбъектМножество;
	
КонецФункции

// add
// 
// Параметры:
//  Множество - См. Множество
//  Элемент - Произвольный - новый элемент множества
// 
// Возвращаемое значение:
//  Булево - Истина - элмент добавлен
Функция Добавить(Множество, Элемент) Экспорт
	
	СоставКлюча = Множество.СоставКлюча;
	
	Если ЗначениеЗаполнено(СоставКлюча) Тогда
		
		Словарь = Множество.Словарь;
		
		Для Индекс = 0 По СоставКлюча.ВГраница() - 1 Цикл
			
			Ключ = СоставКлюча[Индекс];
			ЗначениеКлюча = Элемент[Ключ];
			ЭлементМножества = Словарь[ЗначениеКлюча];
			
			Если ЭлементМножества = Неопределено Тогда
				
				ЭлементМножества = Новый Соответствие;
				Словарь[ЗначениеКлюча] = ЭлементМножества;
				
			КонецЕсли;
			
			Словарь = ЭлементМножества;
			
		КонецЦикла;
		
		Ключ = СоставКлюча[СоставКлюча.ВГраница()];
		ЗначениеКлюча = Элемент[Ключ];
		Значение = Словарь[ЗначениеКлюча];
		
		Если Значение = Неопределено Тогда
			Словарь.Вставить(ЗначениеКлюча, Элемент);
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
				
	КонецЕсли;
	
	Словарь = Множество.Словарь;
	ЗначениеКлюча = Элемент;
	Значение = Словарь[ЗначениеКлюча];
	
	Если Значение = Неопределено Тогда
		Словарь.Вставить(ЗначениеКлюча, Элемент);
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// delete
// 
// Параметры:
//  Множество - см. Множество
//  Элемент - Произвольный
Процедура Удалить(Множество, Элемент) Экспорт
	
	СоставКлюча = Множество.СоставКлюча;
	
	Если ЗначениеЗаполнено(СоставКлюча) Тогда

		СоставСловарей = Новый Массив;
		Словарь = Множество.Словарь;
		СоставСловарей.Добавить(Словарь);
		
		Для Индекс = 0 По СоставКлюча.ВГраница() - 1 Цикл
			
			Ключ = СоставКлюча[Индекс];
			ЗначениеКлюча = Элемент[Ключ];
			ЭлементМножества = Словарь[ЗначениеКлюча];
			
			Если ЭлементМножества = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			Словарь = ЭлементМножества;
			СоставСловарей.Добавить(Словарь);
			
		КонецЦикла;
		
		Ключ = СоставКлюча[СоставКлюча.ВГраница()];
		ЗначениеКлюча = Элемент[Ключ];
		Словарь.Удалить(ЗначениеКлюча);
		
		Для Индекс = -СоставСловарей.ВГраница() По 0 Цикл
			
			Словарь = СоставСловарей[-Индекс];
			
			Если Словарь.Количество() > 1 Тогда
				Прервать;
			КонецЕсли;
			
			Словарь.Очистить();
			
		КонецЦикла;
		
		Возврат;
				
	КонецЕсли;
	
	Множество.Словарь.Удалить(Элемент);
	
КонецПроцедуры

// has
// 
// Параметры:
//  Множество - см. Множество:
//  Элемент - Произвольный
//  Значение - Произвольный, Неопределено - если елемент есть, то возвращается он же, иначе Неопределено
// 
// Возвращаемое значение:
//  Булево - Содержит
Функция Содержит(Множество, Элемент, Значение = Неопределено) Экспорт
	
	СоставКлюча = Множество.СоставКлюча;
	
	Если ЗначениеЗаполнено(СоставКлюча) Тогда

		Словарь = Множество.Словарь;
		
		Для Индекс = 0 По СоставКлюча.ВГраница() - 1 Цикл
			
			Ключ = СоставКлюча[Индекс];
			ЗначениеКлюча = Элемент[Ключ];
			ЭлементМножества = Словарь[ЗначениеКлюча];
			
			Если ЭлементМножества = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Словарь = ЭлементМножества;
			
		КонецЦикла;
		
		Ключ = СоставКлюча[СоставКлюча.ВГраница()];
		ЗначениеКлюча = Элемент[Ключ];
		Значение = Словарь[ЗначениеКлюча];
		Возврат Значение <> Неопределено;
				
	КонецЕсли;
	
	ЗначениеКлюча = Элемент;
	Значение = Множество.Словарь[ЗначениеКлюча];
	Возврат Значение <> Неопределено;
	
КонецФункции

// isEmpty
// 
// Параметры:
//  Множество - см. Множество
// 
// Возвращаемое значение:
//  Булево - Пустое
Функция Пустое(Множество) Экспорт
	Возврат НЕ ЗначениеЗаполнено(Множество.Словарь);
КонецФункции

// Пересечение.
// 
// Параметры:
//  Множество1 - см. Множество
//  Множество2 - см. Множество
// 
// Возвращаемое значение:
//  см. Множество
Функция Пересечение(Множество1, Множество2) Экспорт
	
	Результат = Множество(Множество1.Ключ);
	
	Для Каждого КлючЗначение Из Множество1.Словарь Цикл
		
		Значение = КлючЗначение.Значение;
		
		Если НЕ Содержит(Множество2, Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Добавить(Результат, Значение);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Разность.
// 
// Параметры:
//  Множество1 - см. Множество
//  Множество2 - см. Множество
// 
// Возвращаемое значение:
//  см. Множество
Функция Разность(Множество1, Множество2) Экспорт
	
	Результат = Множество(Множество1.Ключ);
	
	Для Каждого КлючЗначение Из Множество1.Словарь Цикл
		
		Значение = КлючЗначение.Значение;
		
		Если Содержит(Множество2, Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Добавить(Результат, Значение);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Объединение.
// 
// Параметры:
//  Множество1 - см. Множество
//  Множество2 - см. Множество
// 
// Возвращаемое значение:
//  см. Множество
Функция Объединение(Множество1, Множество2) Экспорт
	
	Результат = Множество(Множество1.Ключ);
	
	Для Каждого КлючЗначение Из Множество1.Словарь Цикл
		Добавить(Результат, КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Массив.
// 
// Параметры:
//  Множество - см. Множество
// 
// Возвращаемое значение:
//  Массив из Произвольный
Функция Массив(Множество) Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого КлючЗначение Из Множество.Словарь Цикл
		Результат.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти