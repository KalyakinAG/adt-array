//  Подсистема "Абстрактный тип данных массив"
//	Автор: Калякин Андрей Г.
//  https://github.com/KalyakinAG/adt-array
//  https://infostart.ru/1c/articles/1473034/

#Область ПрограммныйИнтерфейс

Функция Множество(Ключ = Неопределено) Экспорт
	_Множество = Новый Структура("Ключ, Словарь", Ключ, Новый Соответствие);
	Возврат _Множество;
КонецФункции

Функция Дополнить(__Множество = Неопределено, Элементы) Экспорт
	_Множество = ?(__Множество = Неопределено ИЛИ ТипЗнч(__Множество) = Тип("Строка"), Множество(__Множество), __Множество);
	Для Каждого Элемент Из Элементы Цикл
		Добавить(_Множество, Элемент);
	КонецЦикла;
	Возврат _Множество;
КонецФункции

//  add
Процедура Добавить(Множество, Элемент) Экспорт
	ЗначениеКлюча = ?(ЗначениеЗаполнено(Множество.Ключ), Элемент[Множество.Ключ], Элемент);
	Множество.Словарь.Вставить(ЗначениеКлюча, Элемент);
КонецПроцедуры

//  delete
Процедура Удалить(Множество, Элемент) Экспорт
	ЗначениеКлюча = ?(ЗначениеЗаполнено(Множество.Ключ), Элемент[Множество.Ключ], Элемент);
	Множество.Словарь.Удалить(ЗначениеКлюча);
КонецПроцедуры

//  has
Функция Содержит(Множество, Элемент, Значение = Неопределено) Экспорт
	ЗначениеКлюча = ?(ЗначениеЗаполнено(Множество.Ключ), Элемент[Множество.Ключ], Элемент);
	Значение = Множество.Словарь[ЗначениеКлюча];
	Возврат Значение <> Неопределено;
КонецФункции

//  isEmpty
Функция Пустое(Множество) Экспорт
	Возврат НЕ ЗначениеЗаполнено(Множество.Словарь);
КонецФункции

Функция Пересечение(Множество1, Множество2) Экспорт
	Результат = Множество(Множество1.Ключ);
	Для Каждого КлючЗначение Из Множество1.Словарь Цикл
		Значение = КлючЗначение.Значение;
		Если НЕ Содержит(Множество2, Значение) Тогда
			Продолжить;
		КонецЕсли;
		Добавить(Результат, Значение);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция Разность(Множество1, Множество2) Экспорт
	Результат = Множество(Множество1.Ключ);
	Для Каждого КлючЗначение Из Множество1.Словарь Цикл
		Значение = КлючЗначение.Значение;
		Если Содержит(Множество2, Значение) Тогда
			Продолжить;
		КонецЕсли;
		Добавить(Результат, Значение);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция Объединение(Множество1, Множество2) Экспорт
	Результат = Множество(Множество1.Ключ);
	Для Каждого КлючЗначение Из Множество1.Словарь Цикл
		Добавить(Результат, КлючЗначение.Значение);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция Массив(Множество) Экспорт
	Результат = Новый Массив;
	Для Каждого КлючЗначение Из Множество.Словарь Цикл
		Результат.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти