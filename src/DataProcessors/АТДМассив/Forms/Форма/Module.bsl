//  Подсистема "Абстрактный тип данных массив"
//	Автор: Калякин Андрей Г.
//  https://github.com/KalyakinAG/adt-array
//  https://infostart.ru/1c/articles/1473034/

&НаКлиенте
Перем ЭлементыМассива;

// Преобразует переданные элементы в массив 
// 
// Параметры:
//  НовыеЭлементы - массив - Коллекция элементов, для которой доступен обход ДляКаждого. 
//  	Если параметр не задан, то преобразуется текущая коллекция в массив элементов.
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Массив
&НаКлиенте
Функция Массив(НовыеЭлементы = Неопределено) Экспорт
	Если НовыеЭлементы = Неопределено Тогда
		Если ТипЗнч(ЭлементыМассива) = Тип("Массив") Тогда
			Возврат ЭтотОбъект;
		КонецЕсли;		
		ЭлементыМассива = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ЭлементыМассива);
		Возврат ЭтотОбъект;
	КонецЕсли;
	ЭлементыМассива = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(НовыеЭлементы);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Установить(Элементы) Экспорт
	ЭлементыМассива = Элементы;
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция ВМассив() Экспорт
	Если ТипЗнч(ЭлементыМассива) <> Тип("Массив") Тогда
		Массив();
	КонецЕсли;
	Возврат ЭлементыМассива;
КонецФункции

&НаКлиенте
Функция ВСписокЗначений() Экспорт
	Список = Новый СписокЗначений;
	Список.ЗагрузитьЗначения(ВМассив());
	Возврат Список;
КонецФункции

&НаКлиенте
Функция ВФиксированныйМассив() Экспорт
	Возврат Новый ФиксированныйМассив(ВМассив());
КонецФункции

//  filter
&НаКлиенте
Функция Отобрать(ВыражениеПредиката, Контекст = Неопределено, Параметры = Неопределено) Экспорт
	ЭлементыМассива = РаботаСМассивом.Отобрать(ЭлементыМассива, ВыражениеПредиката, Контекст, Параметры);
	Возврат ЭтотОбъект;
КонецФункции

//  find
&НаКлиенте
Функция НайтиЭлемент(ВыражениеПредиката, Контекст = Неопределено, Параметры = Неопределено) Экспорт
	Возврат РаботаСМассивом.НайтиЭлемент(ЭлементыМассива, ВыражениеПредиката, Контекст, Параметры);
КонецФункции

// sortBy
// Пример:
//Фрукты = Общий.АТДМассив(СтрРазделить("вишня, арбузы, бананы", ", ", Ложь));
//Фрукты.Сортировать();
//Фрукты.ДляКаждого("Сообщить(Элемент)");// [арбузы, бананы, вишня]
// 
// Параметры:
//  ВыражениеПредиката - Строка
//  Контекст - Неопределено - Контекст
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Сортировать
&НаКлиенте
Функция СортироватьПо(Поля) Экспорт
	РаботаСМассивом.СортироватьПо(ЭлементыМассива, Поля);
	Возврат ЭтотОбъект;
КонецФункции

// sort
// Пример:
//Фрукты = Общий.АТДМассив(СтрРазделить("вишня, арбузы, бананы", ", ", Ложь));
//Фрукты.Сортировать();
//Фрукты.ДляКаждого("Сообщить(Элемент)");// [арбузы, бананы, вишня]
// 
// Параметры:
//  ВыражениеПредиката - Строка
//  Контекст - Неопределено - Контекст
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Сортировать
&НаКлиенте
Функция Сортировать(ВыражениеПредиката = Неопределено, Контекст = Неопределено, Параметры = Неопределено) Экспорт
	РаботаСМассивом.Сортировать(ЭлементыМассива, ВыражениеПредиката, Контекст, Параметры);
	Возврат ЭтотОбъект;
КонецФункции

// map
// 
// Параметры:
//  Выражение - Строка - Выражение функции
//  Контекст - Неопределено - Контекст
//  Параметры - Неопределено - Параметры
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Отобразить
&НаКлиенте
Функция Отобразить(Выражение, Контекст = Неопределено, Параметры = Неопределено) Экспорт
	ЭлементыМассива = РаботаСМассивом.Отобразить(ЭлементыМассива, Выражение, Контекст, Параметры);
	Возврат ЭтотОбъект;
КонецФункции

// reduce
// 
// Параметры:
//  Выражение - Строка - Выражение функции
//  НачальноеЗначение - Неопределено - Начальное значение
//  Контекст - Неопределено - Контекст
//  Параметры - Неопределено - Параметры
// 
// Возвращаемое значение:
//  Произвольный - Преобразовать
&НаКлиенте
Функция Преобразовать(Выражение, НачальноеЗначение = Неопределено, Контекст = Неопределено, Параметры = Неопределено) Экспорт
	Возврат РаботаСМассивом.Преобразовать(ЭлементыМассива, Выражение, НачальноеЗначение, Контекст, Параметры);
КонецФункции

//  pop
&НаКлиенте
Функция Взять() Экспорт
	Возврат РаботаСМассивом.Взять(ЭлементыМассива);
КонецФункции

//  push
&НаКлиенте
Функция Положить(Элемент) Экспорт
	РаботаСМассивом.Положить(ЭлементыМассива, Элемент);
	Возврат ЭтотОбъект;
КонецФункции

//  add
&НаКлиенте
Функция Добавить(Элемент) Экспорт
	РаботаСМассивом.Добавить(ЭлементыМассива, Элемент);
	Возврат ЭтотОбъект;
КонецФункции

//  insert
&НаКлиенте
Функция Вставить(Индекс, Элемент) Экспорт
	ЭлементыМассива.Вставить(Индекс, Элемент);
	Возврат ЭтотОбъект;
КонецФункции

//  slice
&НаКлиенте
Функция Срез(НижняяГраница = 0, Знач ВерхняяГраница = Неопределено, Шаг = 1) Экспорт
	ЭлементыМассива = РаботаСМассивом.Срез(ЭлементыМассива, НижняяГраница, ВерхняяГраница, Шаг);
	Возврат ЭтотОбъект;
КонецФункции

#Если НЕ ВебКлиент Тогда

//  forEach
&НаКлиенте
Функция ДляКаждого(Алгоритм, Контекст = Неопределено, Параметры = Неопределено) Экспорт
	РаботаСМассивом.ДляКаждого(ЭлементыМассива, Алгоритм, Контекст, Параметры);
	Возврат ЭтотОбъект;
КонецФункции

#КонецЕсли

&НаКлиенте
Функция ДополнитьМассив(МассивИсточник, ТолькоУникальныеЗначения = Ложь) Экспорт
	РаботаСМассивом.ДополнитьМассив(ЭлементыМассива, МассивИсточник, ТолькоУникальныеЗначения);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Пустой() Экспорт
	Возврат ЭлементыМассива.Количество() = 0;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Отказ = Истина;
	Сообщить("Обработка не предназначена для работы в интерактивном режиме!");
КонецПроцедуры

ЭлементыМассива = Новый Массив;
