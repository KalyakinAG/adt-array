Перем Элементы Экспорт;

Функция Массив(НовыеЭлементы) Экспорт
	Элементы = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(НовыеЭлементы);
	Возврат ЭтотОбъект;
КонецФункции

//  filter
Функция Отобрать(Предикат, Контекст = Неопределено) Экспорт
	Элементы = РаботаСМассивом.Отобрать(Элементы, Предикат, Контекст);
	Возврат ЭтотОбъект;
КонецФункции

//  find
Функция НайтиЭлемент(Предикат, Контекст = Неопределено) Экспорт
	Возврат РаботаСМассивом.НайтиЭлемент(Элементы, Предикат, Контекст);
КонецФункции

//  sort
Функция Сортировать(Предикат = Неопределено, Контекст = Неопределено) Экспорт
	РаботаСМассивом.Сортировать(Элементы, Предикат, Контекст);
	Возврат ЭтотОбъект;
КонецФункции

//  map
Функция Отобразить(Выражение, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Элементы = РаботаСМассивом.Отобразить(Элементы, Выражение, Контекст, Данные);
	Возврат ЭтотОбъект;
КонецФункции

//  reduce
Функция Преобразовать(Выражение, НачальноеЗначение = Неопределено, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Возврат РаботаСМассивом.Преобразовать(Элементы, Выражение, НачальноеЗначение, Контекст, Данные);
КонецФункции

//  pop
Функция Взять() Экспорт
	Возврат РаботаСМассивом.Взять(Элементы);
КонецФункции

//  push
Функция Положить(Элемент) Экспорт
	РаботаСМассивом.Положить(Элементы, Элемент);
	Возврат ЭтотОбъект;
КонецФункции

//  forEach
Функция ДляКаждого(Алгоритм, Контекст = Неопределено, Данные = Неопределено) Экспорт
	РаботаСМассивом.ДляКаждого(Элементы, Алгоритм, Контекст, Данные);
	Возврат ЭтотОбъект;
КонецФункции

Элементы = Новый Массив;
