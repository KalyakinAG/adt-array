Перем Элементы Экспорт;

// Преобразует переданные элементы в массив 
// 
// Параметры:
//  НовыеЭлементы - массив - Коллекция элементов, для которой доступен обход ДляКаждого. 
//  	Если параметр не задан, то преобразуется текущая коллекция в массив элементов.
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Массив
Функция Массив(НовыеЭлементы = Неопределено) Экспорт
	Если НовыеЭлементы = Неопределено Тогда
		Если ТипЗнч(Элементы) = Тип("Массив") Тогда
			Возврат ЭтотОбъект;
		КонецЕсли;		
		Элементы = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы);
		Возврат ЭтотОбъект;
	КонецЕсли;
	Элементы = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(НовыеЭлементы);
	Возврат ЭтотОбъект;
КонецФункции

//  filter
Функция Отобрать(Предикат, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Элементы = РаботаСМассивом.Отобрать(Элементы, Предикат, Контекст, Данные);
	Возврат ЭтотОбъект;
КонецФункции

//  find
Функция НайтиЭлемент(Предикат, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Возврат РаботаСМассивом.НайтиЭлемент(Элементы, Предикат, Контекст, Данные);
КонецФункции

// sort
// Пример:
//Фрукты = Общий.АТДМассив(СтрРазделить("вишня, арбузы, бананы", ", ", Ложь));
//Фрукты.Сортировать();
//Фрукты.ДляКаждого("Сообщить(Элемент)");// [арбузы, бананы, вишня]
// 
// Параметры:
//  Предикат - Неопределено - Предикат
//  Контекст - Неопределено - Контекст
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Сортировать
Функция Сортировать(Предикат = Неопределено, Контекст = Неопределено) Экспорт
	РаботаСМассивом.Сортировать(Элементы, Предикат, Контекст);
	Возврат ЭтотОбъект;
КонецФункции

// map
// 
// Параметры:
//  Выражение Выражение
//  Контекст - Неопределено - Контекст
//  Данные - Неопределено - Данные
// 
// Возвращаемое значение:
//  ОбработкаОбъект.АТДМассив - Отобразить
Функция Отобразить(Выражение, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Элементы = РаботаСМассивом.Отобразить(Элементы, Выражение, Контекст, Данные);
	Возврат ЭтотОбъект;
КонецФункции

// reduce
// 
// Параметры:
//  Выражение Выражение
//  НачальноеЗначение - Неопределено - Начальное значение
//  Контекст - Неопределено - Контекст
//  Данные - Неопределено - Данные
// 
// Возвращаемое значение:
//  Произвольный - Преобразовать
Функция Преобразовать(Выражение, НачальноеЗначение = Неопределено, Контекст = Неопределено, Данные = Неопределено) Экспорт
	Возврат РаботаСМассивом.Преобразовать(Элементы, Выражение, НачальноеЗначение, Контекст, Данные);
КонецФункции

//  pop
Функция Взять() Экспорт
	Возврат РаботаСМассивом.Взять(Элементы);
КонецФункции

//  push
Функция Положить(Элемент) Экспорт
	РаботаСМассивом.Положить(Элементы, Элемент);
	Возврат ЭтотОбъект;
КонецФункции

//  forEach
Функция ДляКаждого(Алгоритм, Контекст = Неопределено, Данные = Неопределено) Экспорт
	РаботаСМассивом.ДляКаждого(Элементы, Алгоритм, Контекст, Данные);
	Возврат ЭтотОбъект;
КонецФункции

Элементы = Новый Массив;
